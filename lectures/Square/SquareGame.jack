// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/11/Square/SquareGame.jack

// (same as projects/09/Square/SquareGame.jack)

/**
 * Implements the Square Dance game.
 * This simple game allows the user to move a black square around
 * the screen, and change the square's size during the movement.
 * When the game starts, a square of 30 by 30 pixels is shown at the
 * top-left corner of the screen. The user controls the square as follows.
 * The 4 arrow keys are used to move the square up, down, left, and right.
 * The 'z' and 'x' keys are used, respectively, to decrement and increment
 * the square's size. The 'q' key is used to quit the game.
 */

class SquareGame {
   field Square square; // the square of this game
   field int direction; // the square's current direction: 
                        // 0=none, 1=up, 2=down, 3=left, 4=right

   /** Constructs a new Square Game. */
   constructor SquareGame new() {
      // Creates a 30 by 30 pixels square and positions it at the top-left
      // of the screen.
      let square = Square.new(0, 0, 30);
      let direction = 0;  // initial state is no movement
      return this;
   }

   /** Disposes this game. */
   method void dispose() {
      do square.dispose();
      do Memory.deAlloc(this);
      return;
   }

   /** Moves the square in the current direction. */
   method void moveSquare() {
      if (direction = 1) { do square.moveUp(); }
      if (direction = 2) { do square.moveDown(); }
      if (direction = 3) { do square.moveLeft(); }
      if (direction = 4) { do square.moveRight(); }
      do Sys.wait(5);  // delays the next movement
      return;
   }

   /** Runs the game: handles the user's inputs and moves the square accordingly */
   method void run() {
      var char key;  // the key currently pressed by the user
      var boolean exit;
      let exit = false;
      
      while (~exit) {
         // waits for a key to be pressed
         while (key = 0) {
            let key = Keyboard.keyPressed();
            do moveSquare();
         }
         if (key = 81)  { let exit = true; }     // q key
         if (key = 90)  { do square.decSize(); } // z key
         if (key = 88)  { do square.incSize(); } // x key
         if (key = 131) { let direction = 1; }   // up arrow
         if (key = 133) { let direction = 2; }   // down arrow
         if (key = 130) { let direction = 3; }   // left arrow
         if (key = 132) { let direction = 4; }   // right arrow

         // waits for the key to be released
         while (~(key = 0)) {
            let key = Keyboard.keyPressed();
            do moveSquare();
         }
     } // while
     return;
   }
}


/*

   field Square square; // the square of this game
   field int direction; // the square's current directi

   --> just go in the symbol table as fields.


   // constructor SquareGame new() {
   //    // Creates a 30 by 30 pixels square and positions it at the top-left
   //    // of the screen.


   function SquareGame.new 0
   // Allocate memory for the new object
   push constant 2    // 2 = # of fields
   call Memory.alloc 1
   pop pointer 0      // set THIS to address of the newly allocated object
                      // now 'this 0' = square field, 'this 1' = direction field.

   //    let square = Square.new(0, 0, 30);
   push constant 0
   push constant 0
   push constant 30
   call Square.new 3
   pop this 0         // call Square.new and store result in square = this.0
                      // Note we don't have to worry about call to Square.new
                      // clobbering this, because VM function calls save & restore it.

   //    let direction = 0;  // initial state is no movement
   push constant 0
   pop this 1

   //    return this;
   push pointer 0     // push value of this
                      // 'this' is a keyword constant which is always
                      // compiled to  'push pointer 0'
   return

   // }


   // method void dispose() {

   function SquareGame.dispose 0

   // Since this is a method, it secretly has 1 argument!
   // Note with a method, we have to start numbering arguments in the symbol table from 1

   push argument 0
   pop pointer 0    // save the argument into THIS

   //    do square.dispose();
   push this 0
   call Square.dispose 1

   //    do Memory.deAlloc(this);

   push pointer 0
   call Memory.deAlloc 1

   //    return;

   push constant 0
   return

   // }



// Function call without a dot is always a method call on current object.
// Just pass THIS as first argument.
// e.g.

//   do moveSquare();

push pointer 0                // put THIS on stack
call SquareGame.moveSquare 1  // SquareGame = name of current class
pop temp 0


*/